buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.27'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = modVersion
group = modGroup
archivesBaseName = modBaseName


compileJava {
    options.compilerArgs += [
            '-Aelegantnetworking.printDetails',
            '-Aelegantnetworking.printDebug'
    ]
}


sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20190719-1.14.3'

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                    source project(':_ElegantNetworkingCommon').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                    source project(':_ElegantNetworkingCommon').sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/')

            mods {
                examplemod {
                    source sourceSets.main
                    source project(':_ElegantNetworkingCommon').sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
    maven {
        name = "chickenbones"
        url = "https://chickenbones.net/maven" //or https://maven.covers1624.net/
    }
}

configurations {
    extraLibs { transitive = false }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.14.4-28.2.23'

    implementation fg.deobf("codechicken:CodeChickenLib:1.14.4-3.2.8.374:universal")

    implementation(project(':_ElegantNetworkingCommon')) { transitive = false }
    extraLibs project(':_ElegantNetworkingCommon')
    
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8', project(':ElegantNetworkingAnnotationProcessor')
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    if (!testBuild) {
        exclude('hohserg/elegant/networking/test/**')
        exclude('META-INF/services/**')
        exclude('tmp_generated/**')
    }
    manifest {
        attributes([
                "Specification-Title"     : "elegant_networking",
                "Specification-Vendor"    : "elegant_networking",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "elegant_networking",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    exclude('hohserg/elegant/networking/test/**')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    exclude('hohserg/elegant/networking/test/**')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            def localRepo = new File(project.getProjectDir(), 'repo')
            localRepo.deleteDir()

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            def localRepoUrl = localRepo.getAbsolutePath()

            def usedLocation = releasesRepoUrl
            url = usedLocation

            if (usedLocation != localRepoUrl && project.hasProperty("ossrhUsername")) {
                credentials {
                    username = "$ossrhUsername"
                    password = "$ossrhPassword"
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId 'elegant-networking-1.7.10'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'ElegantNetworking MinecraftForge 1.12 library'
                packaging = 'jar'
                description = 'This is a runtime library of ElegantNetworking for MinecraftForge 1.12'
                url = 'https://github.com/ElegantNetworking/ElegantNetworkingRoot'

                scm {
                    connection = 'scm:git:https://github.com/ElegantNetworking/ElegantNetworking_1.12.git'
                    developerConnection = 'scm:git:git@github.com:https://github.com/ElegantNetworking/ElegantNetworking_1.12.git'
                    url = 'https://github.com/ElegantNetworking/ElegantNetworking_1.12'
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://raw.githubusercontent.com/ElegantNetworking/ElegantNetworking_1.12/master/LICENSE.md'
                    }
                }

                developers {
                    developer {
                        id = 'hohserg'
                        name = 'hohserg'
                        email = 'hohserg1@yandex.ru'
                    }
                }
            }
        }
    }
}

signing {
    required { project.hasProperty("signing.keyId") }
    sign publishing.publications.mavenJava
}